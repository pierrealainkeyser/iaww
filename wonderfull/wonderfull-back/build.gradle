plugins {
	id 'org.springframework.boot' version '2.2.6.RELEASE'
}

repositories {
	mavenCentral()
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'eclipse'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
springBoot {
	buildInfo()
}
sourceCompatibility = 11
targetCompatibility = 11

task copyNpmDist(type: Copy) {
	from project(':wonderfull-front').fileTree("dist")
	into file("${project.buildDir}/resources/main/dist")
}

def buildFront = tasks.getByPath(':wonderfull-front:build')

bootJar.dependsOn copyNpmDist
bootJar.dependsOn buildFront
copyNpmDist.mustRunAfter buildFront

bootJar {
	baseName  = 'iaww'
}


dependencies {
	implementation(
			'org.springframework.boot:spring-boot-starter-websocket',
			'org.springframework.boot:spring-boot-starter-security',
			'org.springframework.boot:spring-boot-starter-jdbc',
			'org.springframework.boot:spring-boot-starter-actuator',
			'org.springframework.security:spring-security-oauth2-client',
			'org.springframework.security:spring-security-oauth2-jose',
			'org.springframework.security:spring-security-messaging',
			'org.flywaydb:flyway-core',
			'com.h2database:h2'
			)

	testImplementation(
			'org.springframework.boot:spring-boot-starter-test'
			)
}

test {
	useJUnitPlatform()
}
